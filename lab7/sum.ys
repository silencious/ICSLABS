/* Name: Tian Jiahe */
/* ID: 5130379056 */

# Execution begins at address 0 
	.pos 0 
init:
	irmovl Stack,%esp  	# Set up stack pointer  
	rrmovl %esp,%ebp  	# Set up base pointer  
    irmovl ele1,%eax
    pushl %eax 
    call sum_list
    halt

# Sample linked list
.align 4
ele1:
    .long 0x00a
    .long ele2
ele2:
    .long 0x0b0
    .long ele3
ele3:
    .long 0xc00
    .long 0	


# int sum_list(list_ptr ls)
sum_list:
	pushl %ebp
	rrmovl %esp,%ebp
    xorl %eax,%eax
	mrmovl 8(%ebp),%edx
    andl %edx,%edx 
	je     End
Loop:
	mrmovl (%edx),%ecx
	addl %ecx,%eax
    mrmovl 4(%edx),%edx
    andl %edx,%edx 
	jne    Loop
End:
	rrmovl %ebp,%esp
	popl %ebp
	ret

# The stack starts here and grows to lower addresses
	.pos 0x100		
Stack:	 
